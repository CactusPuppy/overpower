globalvar Boolean DEBUG_MODE = WorkshopSettingToggle("99. DEBUG MODE", "DEBUG MODE ENABLED", true, -100);

import "lib/OSTWUtils/Diagnostics.del";
rule: "Create DEBUG huds"
if (DEBUG_MODE)
{
  new Diagnostics(
    VisibleToHostOnly:      true,
    ShowServerLoad:         true,
    ShowServerLoadPeak:     true,
    ShowEntityCount:        true,
    PlayMoreEffectsEnabled: true,
  );
  WaitForeverUntil(IsGameInProgress());
  LogToInspector($"Effect Count: {EntityCount()}");
}

rule: "[debug.del] When host player melees in debug mode, issue an order to advance game state"
if (DEBUG_MODE)
if (HostPlayer().IsButtonHeld(Button.Melee))
{
  switch (currentGameState) {
    case GameState.SELECTING_HEROES:
      GSM_Trigger(GameStateAction.SELECTING_HEROES_COMPLETE);
      break;
    case GameState.GAMEPLAY:
      GSM_Trigger(GameStateAction.TEAM_LSF_WIN);
      break;
    case GameState.CONCLUSION:
      GSM_Trigger(GameStateAction.CONCLUSION_COMPLETE);
      break;
  }
  Wait(0.064);
}

rule: "[debug.del] Reduce match time in assembling heroes"
if (DEBUG_MODE)
if (IsAssemblingHeroes())
{
  SetMatchTime(5);
}

rule: "[debug.del] Give ult faster"
Event.OngoingPlayer
if (DEBUG_MODE)
if (HasSpawned())
if (UltimateChargePercent() < 100)
{
  SetUltimateCharge(EventPlayer(), 100);
  Wait(0.25);
  LoopIfConditionIsTrue();
}
